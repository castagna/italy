PREFIX rdf:              <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs:             <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:              <http://www.w3.org/2002/07/owl#>
PREFIX xsd:              <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf:             <http://xmlns.com/foaf/0.1/>
PREFIX skos:             <http://www.w3.org/2004/02/skos/core#>
PREFIX cc:               <http://creativecommons.org/ns#>
PREFIX dcterms:          <http://purl.org/dc/terms/>
PREFIX wgs84_pos:        <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX geo:              <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX geonames:         <http://www.geonames.org/ontology#>
PREFIX eurostat:         <http://ec.europa.eu/eurostat/ramon/ontologies/geographic.rdf#>
PREFIX nuts:             <http://ec.europa.eu/eurostat/ramon/rdfdata/nuts2008/>
PREFIX italy:            <http://data.kasabi.com/dataset/italy/>
PREFIX java:             <java:com.kasabi.labs.arq.>
PREFIX italy-schema:     <http://data.kasabi.com/dataset/italy/schema/>


CONSTRUCT {
  ?region_uri rdf:type italy-schema:Region .
  ?region_uri rdf:type ?region_type .
  ?region_uri rdfs:label ?region_label .
  ?region_uri rdfs:label ?region_label_en .
  ?region_uri italy-schema:contains ?province_uri .
  ?region_uri geo:lat ?region_lat_dt .
  ?region_uri geo:long ?region_long_dt .
  ?region_uri geonames:population ?region_population_dt .
  ?region_uri owl:sameAs ?region_see_also .
  ?region_uri owl:sameAs ?r .
  ?region_uri geonames:countryCode ?region_country_code .
  ?region_uri geonames:featureClass ?region_feature_class .
  ?region_uri geonames:featureCode ?region_feature_code .
  ?region_uri geonames:parentCountry ?region_parent_country .
  ?region_uri geonames:parentFeature ?region_parent_feature .
  ?region_uri foaf:page ?region_wikipedia_article .
  ?region_uri eurostat:regionCode ?region_nuts_code .
  ?region_uri italy-schema:iso3166-2 ?iso_region_code .
  
  ?province_uri rdf:type italy-schema:Province .
  ?province_uri rdf:type ?province_type .
  ?province_uri rdfs:label ?province_label .
#  ?province_uri italy-schema:contains ?comune_uri .
  ?province_uri italy-schema:isContained ?region_uri .
  ?province_uri geo:lat ?province_lat_dt .
  ?province_uri geo:long ?province_long_dt .
  ?province_uri geonames:population ?province_population_dt .
  ?province_uri owl:sameAs ?province_see_also .
  ?province_uri owl:sameAs ?p .
  ?province_uri geonames:countryCode ?province_country_code .
  ?province_uri geonames:featureClass ?province_feature_class .
  ?province_uri geonames:featureCode ?province_feature_code .  
  ?province_uri geonames:parentCountry ?province_parent_country .
  ?province_uri geonames:parentFeature ?province_parent_feature .
  ?province_uri foaf:page ?province_wikipedia_article .
  ?province_uri eurostat:regionCode ?province_nuts_code .

#  ?comune_uri rdf:type italy-schema:Comune .
#  ?comune_uri rdfs:label ?comune .
#  ?comune_uri italy-schema:isContained ?province_uri .
} WHERE {
  ?r geonames:featureCode geonames:A.ADM1 .
  ?p geonames:featureCode geonames:A.ADM2 .
  ?c geonames:featureCode geonames:A.ADM3 .
  ?p geonames:parentADM1 ?r .
  ?c geonames:parentADM2 ?p .

  ?r geonames:name ?region .
  ?p geonames:name ?province .
  ?c geonames:name ?comune .

  ?r wgs84_pos:lat ?region_lat .
  ?r wgs84_pos:long ?region_long .
  ?r rdf:type ?region_type .

  ?p wgs84_pos:lat ?province_lat .
  ?p wgs84_pos:long ?province_long .
  ?p rdf:type ?province_type .

  ?c wgs84_pos:lat ?comune_lat .
  ?c wgs84_pos:long ?comune_long .
  ?c rdf:type ?comune_type .

  BIND ( strdt ( ?region_lat, xsd:float ) AS ?region_lat_dt ) 
  BIND ( strdt ( ?region_long, xsd:float ) AS ?region_long_dt ) 
  BIND ( strdt ( ?province_lat, xsd:float ) AS ?province_lat_dt ) 
  BIND ( strdt ( ?province_long, xsd:float ) AS ?province_long_dt ) 
  BIND ( strdt ( ?comune_lat, xsd:float ) AS ?comune_lat_dt ) 
  BIND ( strdt ( ?comune_long, xsd:float ) AS ?comune_long_dt ) 

  
  ?r geonames:population ?region_population .
  ?p geonames:population ?province_population .
  ?c geonames:population ?comune_population .
  BIND ( strdt ( ?region_population, xsd:integer ) AS ?region_population_dt ) 
  BIND ( strdt ( ?province_population, xsd:integer ) AS ?province_population_dt ) 
  BIND ( strdt ( ?comune_population, xsd:integer ) AS ?comune_population_dt ) 

  OPTIONAL {
    ?r rdfs:seeAlso ?region_see_also .
  }
  OPTIONAL {
    ?p rdfs:seeAlso ?province_see_also .
  }
  OPTIONAl {
    ?c rdfs:seeAlso ?comune_see_also .
  }

  ?r geonames:countryCode ?region_country_code .
  ?r geonames:featureClass ?region_feature_class .
  ?r geonames:featureCode ?region_feature_code .
  ?r geonames:parentCountry ?region_parent_country .
  ?r geonames:parentFeature ?region_parent_feature .
  OPTIONAL {
    ?r geonames:wikipediaArticle ?region_wikipedia_article .
    FILTER strstarts ( str ( ?region_wikipedia_article ), "http://en." ) .
  }
  ?p geonames:countryCode ?province_country_code .
  ?p geonames:featureClass ?province_feature_class .
  ?p geonames:featureCode ?province_feature_code .
  ?p geonames:parentCountry ?province_parent_country .
  ?p geonames:parentFeature ?province_parent_feature .
  OPTIONAL {
    ?p geonames:wikipediaArticle ?province_wikipedia_article .
    FILTER strstarts ( str ( ?province_wikipedia_article ), "http://en." ) .
  }
  ?c geonames:countryCode ?comune_country_code .
  ?c geonames:featureClass ?comune_feature_class .
  ?c geonames:featureCode ?comune_feature_code .
  ?c geonames:parentCountry ?comune_parent_country .
  ?c geonames:parentFeature ?comune_parent_feature .
  OPTIONAL {
    ?c geonames:wikipediaArticle ?comune_wikipedia_article .
    FILTER strstarts ( str ( ?comune_wikipedia_article ), "http://en." ) .
  }

  OPTIONAL {
    ?r geonames:officialName ?region_official_name .
    FILTER langmatches( lang(?region_official_name), "en") .
    BIND ( ?region_official_name AS ?region_label_en )
  } 

  BIND ( strlang( java:replace( ?province, "Provincia di |Province of ", "" ), "it" ) AS ?province_label )
  BIND ( strlang( java:replace( java:replace( ?region, "Region autonoma della |Regione Autonoma |Regione ", "" ), "Siciliana", "Sicilia" ), "it" ) AS ?region_label )
  
  BIND ( java:replace( java:replace( java:replace ( lcase( ?region ), "regione autonoma della |regione autonoma |regione ", "" ), " - | ", "-" ), "siciliana", "sicilia" ) AS ?region_path )
  BIND ( java:replace ( java:replace( java:replace ( lcase( ?province ), "province of |provincia di ", "" ), " - | ", "-" ), "forl√¨", "forli-cesena" ) AS ?province_path )
  BIND ( java:replace( lcase( ?comune ), " - | ", "-" ) AS ?comune_path )
  
  BIND ( iri( concat( "http://data.kasabi.com/dataset/italy/", ?region_path ) ) AS ?region_uri )
  BIND ( iri( concat( "http://data.kasabi.com/dataset/italy/", ?province_path ) ) AS ?province_uri )
  BIND ( iri( concat( "http://data.kasabi.com/dataset/italy/", ?comune_path ) ) AS ?comune_uri )
#  BIND ( iri( concat( "http://data.kasabi.com/dataset/italy/", ?region_path, '/', ?province_path ) ) AS ?province_uri )
#  BIND ( iri( concat( "http://data.kasabi.com/dataset/italy/", ?region_path, '/', ?province_path, '/', ?comune_path ) ) AS ?comune_uri )
  
  OPTIONAL {
    ?eurostat_region rdfs:seeAlso ?region_uri .
    ?eurostat_region eurostat:regionCode ?region_nuts_code .
  }
  
  OPTIONAL {
    ?eurostat_province rdfs:seeAlso ?province_uri .
    ?eurostat_province eurostat:regionCode ?province_nuts_code .
  }

#
#  This goes terribly slow...
#
#  OPTIONAL {
#    ?geonames_region_uri geonames:name ?iso_region_label .
#    ?iso_region rdfs:label ?iso_region_label .
#    ?iso_region skos:notation ?iso_region_code .
#  }
  
}
ORDER BY ?region ?province ?comune
